def Step <N> :=
    lambda (x, velocity) {NTensorT <N, Bit> * Bit} ->
    ctrl {Maybe <Bit>, (NTensorT <N, Bit> * Bit) * (NTensorT <N, Bit> * Bit)} (
        match {NTensorT <N, Bit> * Bit, Maybe <Bit>} (x, velocity) [
            (NTensorE <N, Bit0>, Bit0) -> Just <Bit> of Bit0;
            (NTensorE <N, Bit1>, Bit1) -> Just <Bit> of Bit1;
            else -> Nothing <Bit>;
        ])[
        Just <Bit> of Bit0 -> ((x, velocity), (IncrementLE <N> of NTensorE <N, Bit0>, Bit1));
        Just <Bit> of Bit1 -> ((x, velocity), (DecrementLE <N> of NTensorE <N, Bit1>, Bit0));
        Nothing <Bit> -> ((x, velocity), ctrl {Bit, NTensorT <N, Bit> * Bit} velocity [
            Bit0 -> (DecrementLE <N> of x, velocity);
            Bit1 -> (IncrementLE <N> of x, velocity)
        ])
    ] |> Snd <NTensorT <N, Bit> * Bit, NTensorT <N, Bit> * Bit>
end

def Walk <N, Nsteps> :=
    if Nsteps = [0] then
        Id <NTensorT <N, Bit> * Bit>
    else
        lambda (x, velocity) {NTensorT <N, Bit> * Bit} ->
        (x, velocity) |> Step <N> |> Walk <N, [Nsteps - 1]>
    endif
end

def N := [3] end
def Nsteps := [1] end

(NTensorE <N, BitPlus>, Bit0 |> u3 {pi/4, 0, 0})
|> Walk <N, Nsteps>
|> Fst <NTensorT <N, Bit>, Bit>
|> Reverse <N, Bit>
