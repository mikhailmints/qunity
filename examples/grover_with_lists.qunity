/*
Equal superposition of List{#n, Bit}
*/
def $equal_superpos_list{#n} : List{#n, Bit} :=
    if #n = 0 then
        $ListEmpty{0, Bit}
    else
        // There are 2 ^ (#n + 1) - 1 possible states of List {#n, Bit}.
        // One of them is the empty list and the others have $0 or $1
        // as the first element. First, create a superposition of a qubit
        // representing the amplitudes of these 3 possibilities.
        $0
        |> u3{2 * arccos(sqrt(1 / (2 ^ (#n + 1) - 1))), 0, 0}
        // Build the state, controlling on the qubit and recursively constructing
        // the equal superposition of the remaining bits in the case where the
        // list is not empty.
        |> lambda m -> ctrl m [
            $0 -> (m, $ListEmpty{#n, Bit});
            $1 -> (m, @ListCons{#n, Bit}($plus, $equal_superpos_list{#n - 1}))
        ]
        // Use the specialized erasure pattern to erase the qubit.
        |> lambda (ctrl l [
            $ListEmpty{#n, Bit} -> ($0, l);
            @ListCons{#n, Bit}(x, l') -> ($1, l)
        ]) -> l
    endif
end

/*
Quantum oracle representing a function acting on a list of n bits that outputs
1 if the sum of the elements is odd
*/
def @is_odd_sum{#n} : List{#n, Bit} -> Bit :=
    if #n = 0 then
        lambda l -> $0
    else
        lambda l -> match l [
            $ListEmpty{#n, Bit} -> $0;
            @ListCons{#n, Bit}($0, l') -> @is_odd_sum{#n - 1}(l');
            @ListCons{#n, Bit}($1, l') -> @not(@is_odd_sum{#n - 1}(l'));
        ]
    endif
end

def #n := 2 end

$grover{List{#n, Bit}, $equal_superpos_list{#n}, @is_odd_sum{#n}, 1}
